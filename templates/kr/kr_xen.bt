// Archive format for Karaoke Revolution on 360 (American Idol Encore)
// Minimal size = 8192

BigEndian();

/*
Tags:
 Name  | Prefix | Extension | Type
  _STR |        |  .str     |  Stream
  @STR |    __  |   __      |  Stream Index?
  ACTR |    o_  |           |  Actor
  ASTR |        |  .wav     |  Audio Stream Header?
  MTRL |  mat_  |           |  Material
  PSHR |   ps_  |           |  Pixel Shader?
  SAMP
  SBNK
  SPAT
  TEXR |    t_  |           |  Texture
  VSHR |   vs_  |           |  Vertex Shader?
*/

struct XenFile;

typedef struct (uint str_table_start, uint tag_table_start) // TODO: Pass in XenFile& instead
{
    uint block_offset; // Multiply by block size to get index
    uint hash<format=hex>;
    uint file_size_1;
    uint file_name_str_index;

    uint entry_type; // 0 = sys/dummy, 1 = file
    uint tag_index;
    uint file_size_2;
    uint unknown<format=hex>; // Sometimes 0?

    local string file_name = ReadString(str_table_start + file_name_str_index);
    local string tag = "SYSTEM";

    if (entry_type > 0)
    {
        tag = ReadString(tag_table_start + tag_index, 4);
    }
} FileEntry<read=Str("%s (%s)", this.file_name, this.tag)>;

typedef struct XenFile
{
    uint unknown<format=hex>; // Checksum maybe?
    uint chunk_size; // Always 4096

    uint counts[13];

    //  0 - Some count? 0, 4, 22
    //    0 - Regular files?
    //    4 -
    //   22 -
    //  1 - File entry count 
    //  2 - File entry chunk index
    //  * Each entry is 32 bytes in file entry chunk *
    
    // TagTable.pak.sys (Not always present)
    //  3 - Unknown chunk index
    //  4 - Tag count (4 chars per tag) 3, 5

    //  5 - ??? 0

    // Updated:
    //  6 - Chunk index of file info?
    //    File structure: int[] { 0, total_chunk_count, 1 }
    //    ** Little endian for some reason **
    //    Not in file entries either

    //  6 - Chunk index to last file entry, not always preset
    //  7 - Always 1 (Chunk index to first file?)

    // FilenameTable.pak.sys
    //  8 - String table chunk index
    //  9 - String table size
    // 10 - File entries size

    // 11 - ??? 0
    // 12 - Size of chunk? (2933 index) 140

    // Index order: 5 or 11, 7, 4, 3, 8 (str table), 2, 6

    // String table
    local uint str_table_start = counts[8] * 4096;
    FSeek(str_table_start);
    byte string_table[counts[9]];

    local uint file_count = counts[1];

    local uint tag_table_start = counts[3] * 4096;

    local uint indices[5] = { 7, 4, 3, 2, 6 };
    local uint i;

    for (i = 0; i < 5; i++)
    {
        FSeek(counts[indices[i]] * 4096);

        if (i == 3)
        {
            // Read file entries
            struct FileEntries
            {
                local uint j;

                for (j = 0; j < file_count; j++)
                {
                    FileEntry entries(str_table_start, tag_table_start);
                }
            } file_entries;
            continue;
        }

        struct Chunk
        {
            byte data[4096];
        } chunk;
    }

    // Read files

    FSeek(4096);
    struct Files
    {
        local uint file_offset;
        local uint file_size;

        for (i = 0; i < file_count; i++)
        {
            file_offset = file_entries.entries[i].block_offset * 4096;
            file_size = file_entries.entries[i].file_size_1;

            FSeek(file_offset);

            struct File
            {
                local uint index = i;
                local uint offset = file_offset;
                local uint size = file_size;
                local string name = file_entries.entries[i].file_name;
                local string tag = file_entries.entries[i].tag;

                byte data[size];
            } file<read=Str("%s (%s)", this.name, this.tag)>;
        }
    } files;

} XenFile;


XenFile file;